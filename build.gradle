buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.koin:koin-gradle-plugin:$koin_version"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id "org.beryx.runtime" version "1.12.5"
}

apply plugin: 'kotlin'
apply plugin: 'koin'


group 'hr.ferit.dominikzivko'
version '1.2.0'

mainClassName = 'hr.ferit.dominikzivko.unistat.Launcher'

javafx {
    version = "16"
    modules = ['javafx.controls', 'javafx.fxml']
}

runtime {
    addOptions('--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages')
    addModules('java.rmi', 'java.sql', 'java.logging', 'java.management', 'jdk.jfr', 'jdk.unsupported',
            'java.datatransfer', 'java.instrument', 'java.security.jgss', 'java.desktop', 'java.naming',
            'java.compiler', 'java.scripting', 'java.prefs', 'java.xml')
    launcher {
        noConsole = true
    }
    jpackage {
        installerOptions += ['--win-per-user-install', '--win-shortcut', '--win-menu', '--win-menu-group', 'Unistat',
                             '--install-dir', 'Altline/UniStat', '--win-dir-chooser', '--vendor', 'Altline']
        imageOptions += ['--icon', 'src/main/resources/UniStat.ico']
        jvmArgs = ['-splash:$APPDIR/UniStat-splash.png']
    }
}

tasks.jpackageImage.doLast {
    copy {
        from "src/main/resources"
        include "UniStat-splash.png"
        into "build/jpackage/$project.name/app"
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.apache.logging.log4j:log4j-core:$log4j_version"
    implementation "org.apache.logging.log4j:log4j-api:$log4j_version"
    implementation "org.jetbrains.exposed:exposed-core:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-dao:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-java-time:$exposed_version"
    implementation "org.xerial:sqlite-jdbc:$sqlite_version"
    implementation "org.slf4j:slf4j-nop:$slf4j_version"
    implementation "com.jfoenix:jfoenix:$jfoenix_version"
    implementation "net.sourceforge.htmlunit:htmlunit:$htmlunit_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlin_serialization_version"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

run {
    environment += [ 'project_version': "$project.version" ]
    jvmArgs = [
            '--add-exports=javafx.base/com.sun.javafx.binding=ALL-UNNAMED',
            '--add-exports=javafx.base/com.sun.javafx.event=ALL-UNNAMED',
            '--add-exports=javafx.controls/com.sun.javafx.scene.control.behavior=ALL-UNNAMED',
            '--add-exports=javafx.graphics/com.sun.javafx.stage=ALL-UNNAMED'
    ]
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}